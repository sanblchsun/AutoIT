#include <Array.au3>

; Создание тестового массива
Local $a[1000] = [999] ; массив на 1000 элементов, в первом элементе, индекс которого [0], находится число элементов
$DeleteItem = 1 ; число, ячейки с которым будут удалены из массива

For $i = 1 To 999
	$a[$i] = Random(1, 10, 1)
Next

; Начало операции удаления ячеек, значение которых равно 1
$n = 0
For $i = 1 To $a[0]
	If $a[$i] = $DeleteItem Then ; вычисляем индекс от которого выполнять перезапись массива
		$n = $i
		ExitLoop
	EndIf
Next

_ArrayDisplay($a, $a[0] & ' ячеек до удаления, ' & $n & ' - позиция начала перезаписи')

If $n Then ; Если найдено, то приступаем к удалению
	For $i = $n + 1 To $a[0] ; Начинаем от индекса следующего за индексом $n переписывать массив
		If $a[$i] <> $DeleteItem Then ; удаление ячеек, значение которых равно 1
			$a[$n] = $a[$i]
			$n += 1
		EndIf
	Next
	ReDim $a[$n]
	$a[0] = $n - 1
EndIf

_ArrayDisplay($a, $a[0] & ' ячеек после удаления')